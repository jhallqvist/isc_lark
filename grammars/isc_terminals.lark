%import common (HEXDIGIT, WORD, DIGIT, WS)

UINT8: /(25[0-5]|2[0-4][0-9]|1[0-9]{2}|[1-9][0-9]|[0-9])/
UINT16: /(6553[0-5]|655[0-2][0-9]|65[0-4][0-9]{2}|6[0-4][0-9]{3}|[1-5][0-9]{4}|[1-9][0-9]{1,3}|[0-9])/
UINT32: /(429496729[0-5]|42949672[0-8][0-9]|4294967[01][0-9]{2}|429496[0-6][0-9]{3}|42949[0-5][0-9]{4}|4294[0-8][0-9]{5}|429[0-3][0-9]{6}|42[0-8][0-9]{7}|4[01][0-9]{8}|[1-3][0-9]{9}|[1-9][0-9]{1,8}|[0-9])/

IP4_ADDR: (UINT8 ".")~ 1..3 UINT8 // might need to use a different one for the prefix to allow 1 to 4 octets
IPV4_CIDR: /(3[0-2]|[1-2][0-9]|[0-9])/
IPV4_PREFIX: IP4_ADDR "/" IPV4_CIDR

H16: HEXDIGIT~1..4
LS32: (H16 ":" H16 | IP4_ADDR)
IPV6_ADDR1: (H16 ":" )~ 6 LS32
IPV6_ADDR2: "::" (H16 ":")~ 5 LS32
IPV6_ADDR3: H16? "::" (H16 ":")~ 4 LS32
IPV6_ADDR4: ((H16 ":")~ 0..1 H16)? "::" (H16 ":")~ 3 LS32
IPV6_ADDR5: ((H16 ":")~ 0..2 H16)? "::" (H16 ":")~ 2 LS32
IPV6_ADDR6: ((H16 ":")~ 0..3 H16)? "::" H16 ":" LS32
IPV6_ADDR7: ((H16 ":")~ 0..4 H16)? "::" LS32
IPV6_ADDR8: ((H16 ":")~ 0..5 H16)? "::" H16
IPV6_ADDR9: ((H16 ":")~ 0..6 H16)? "::"
IPV6_ADDR: ( IPV6_ADDR1 | IPV6_ADDR2 | IPV6_ADDR3 | IPV6_ADDR4 | IPV6_ADDR5 | IPV6_ADDR6 | IPV6_ADDR7 | IPV6_ADDR8 ) | IPV6_ADDR9
IPV6_CIDR: /(12[0-8]|1[01][0-9]|[1-9][0-9]|[0-9])/
IPV6_PREFIX: IPV6_ADDR "/" IPV6_CIDR

IP_ADDR: ( IP4_ADDR | IPV6_ADDR )
IP_PREFIX: ( IPV4_PREFIX | IPV6_PREFIX )

REGULAR_WORD: ( WORD | DIGIT | "_" )+
QUOTED_STRING: /("[^"]*?"|'[^']*?')/
KEY_ID: "key" WS* REGULAR_WORD
TLS_ID: "tls" WS* REGULAR_WORD
BOOLEAN: ( "true" | "false" | "1" | "0" | "yes" | "no" )
INT_OR_UNLIMITED: ( UINT32 | "unlimited" )
DSCP: /(6[0-3]|[1-5][0-9]|[0-9])/

NEGATED: "!"
WILDCARD: "*"
OCTAL: "0".."7"
AML_PREDEFINED: ( "none" | "any" | "localhost" | "localnets" )
LEFT_BRACE: "{"
RIGHT_BRACE: "}"
SEMICOLON: ";"
